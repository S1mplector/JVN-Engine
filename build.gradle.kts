plugins {
  java
}

allprojects {
  repositories {
    mavenLocal()
    mavenCentral()
  }
}

subprojects {
  apply(plugin = "java")
  apply(plugin = "maven-publish")

  group = (findProperty("jvnGroup") as String?) ?: "com.jvn"
  version = (findProperty("jvnVersion") as String?) ?: "0.1-SNAPSHOT"

  java {
    toolchain {
      languageVersion.set(JavaLanguageVersion.of((findProperty("javaVersion") as String?)?.toIntOrNull() ?: 21))
    }
  }

  tasks.test {
    useJUnitPlatform()
  }

  dependencies {
    testImplementation(platform("org.junit:junit-bom:5.11.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    implementation("org.slf4j:slf4j-api:2.0.13")
  }

  configurations.all {
    resolutionStrategy.dependencySubstitution {
      substitute(module("com.jvn:core")).using(project(":core"))
      substitute(module("com.jvn:fx")).using(project(":fx"))
      substitute(module("com.jvn:scripting")).using(project(":scripting"))
      substitute(module("com.jvn:audio-integration")).using(project(":audio-integration"))
    }
  }

  extensions.configure<org.gradle.api.publish.PublishingExtension> {
    publications {
      create("mavenJava", org.gradle.api.publish.maven.MavenPublication::class.java) {
        from(components["java"])
      }
    }
  }
}
